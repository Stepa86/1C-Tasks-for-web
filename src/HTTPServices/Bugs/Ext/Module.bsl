
Функция ПолучитьОшибкуGET(Запрос)
	
	номерОшибки = Запрос.ПараметрыURL["string"];
	
	Если номерОшибки = "Ping" Тогда
		Ответ = Новый HTTPСервисОтвет(200);
		Ответ.Заголовки.Вставить("Content-Type","text/html; charset=utf-8");
		Ответ.УстановитьТелоИзСтроки( НСтр( "ru='Поздравляем!!! Подключение выполнено успешно!'" ) );
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		найденнаяОшибка = Справочники._Ошибки.НайтиПоКоду( Число( номерОшибки ) );
		
		Если ЗначениеЗаполнено( найденнаяОшибка ) Тогда
			
			Ответ = Ответ_Ошибка( найденнаяОшибка );
			
		Иначе
			
			Ответ = Ответ_ОшибкаНеНайдена();
			
		КонецЕсли;
		
	Исключение
		
		Ответ = Ответ_ОшибкаВыполнения();
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция Ответ_ОшибкаНеНайдена()
	
	Ответ = Новый HTTPСервисОтвет(404);
	Ответ.Заголовки.Вставить("Content-Type","text/html; charset=utf-8");
	Ответ.УстановитьТелоИзСтроки( НСтр( "ru='Ошибка с таким номером не найдена!'" ) );

	Возврат Ответ;
	
КонецФункции

Функция Ответ_ОшибкаВыполнения()
	
	Ответ = Новый HTTPСервисОтвет(500);
	Ответ.Заголовки.Вставить("Content-Type","text/html; charset=utf-8");
	Ответ.УстановитьТелоИзСтроки( НСтр( "ru='При формировании ответа произошла внутренняя ошибка сервера!'" ) );
	
	Возврат Ответ;
	
КонецФункции

Функция Ответ_НеверныйЗапрос( Знач пИнформацияОбОшибке)

	Ответ = Новый HTTPСервисОтвет(400);
	Ответ.Заголовки.Вставить("Content-Type","text/html; charset=utf-8");
	Ответ.УстановитьТелоИзСтроки( КраткоеПредставлениеОшибки( пИнформацияОбОшибке ) );
	
	Возврат Ответ;

КонецФункции // Ответ_НеверныйЗапрос()


Функция Ответ_Ошибка( Знач пОшибка )
	
	Попытка
		
		таблДок = Новый ТабличныйДокумент;
		Справочники._Ошибки.Печать( таблДок, пОшибка );
		
	Исключение
		
		Возврат Ответ_НеверныйЗапрос( ИнформацияОбОшибке() );
		
	КонецПопытки;
	
	текстHTML = ПолучитьТекстHTMLПоТабличномуДокументу(таблДок);
	
	СсылкаНаЗадачу = "<a href=" + Константы._АдресПубликацииИнформационнойБазы.Получить() + "#" + ПолучитьНавигационнуюСсылку(пОшибка) + ">Перейти в 1С</a>";
	
	текстHTML = стрЗаменить( текстHTML, "</body></html>", СсылкаНаЗадачу + "</body></html>" );
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.Заголовки.Вставить("Content-Type","text/html; charset=utf-8");
	Ответ.УстановитьТелоИзСтроки( текстHTML );
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьТекстHTMLПоТабличномуДокументу(Знач таблДок)
	
	имяФайла = ПолучитьИмяВременногоФайла( "html" );
	
	таблДок.Записать( имяФайла, ТипФайлаТабличногоДокумента.HTML5 );
	
	ЧтениеHTML = Новый ЧтениеHTML;
	ЧтениеHTML.ОткрытьФайл( имяФайла );
	
	ПостроительDOM = Новый ПостроительDOM;
	ДокументHTML = ПостроительDOM.Прочитать(ЧтениеHTML);
	ДокументHTML.Заголовок = таблДок.ИспользуемоеИмяФайла;
	
	текстHTML = ПолучитьТекстHTMLИзОбъектаДокументHTML( ДокументHTML );
	
	ЧтениеHTML.Закрыть();
	
	УдалитьФайлы( имяФайла );
	
	Возврат текстHTML;

КонецФункции

// Получает текст HTML из объекта ДокументHTML.
//
// Параметры:
//  ДокументHTML  - ДокументHTML - документ, из которого будет извлекаться текст.
//
// Возвращаемое значение:
//   Строка   - текст HTML
//
Функция ПолучитьТекстHTMLИзОбъектаДокументHTML(ДокументHTML)
	
	ЗаписьDOM = Новый ЗаписьDOM;
	ЗаписьHTML = Новый ЗаписьHTML;
	ЗаписьHTML.УстановитьСтроку();
	ЗаписьDOM.Записать(ДокументHTML,ЗаписьHTML);
	Возврат ЗаписьHTML.Закрыть();
	
КонецФункции





